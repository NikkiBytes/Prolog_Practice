
% phylogenetic inference
descended_from(P,C) :- descended_from(P,Z), descended_from(Z,C).
nad(X,Y) :- gene(X), gene(Y), not descended_from(X,Y), not descended_from(Y,X).

descended_from_by_speciation(P,C) :- descended_from(P,C), not duplication(C).
descended_from_by_speciation(P,C) :- descended_from_by_speciation(P,Z), descended_from_by_speciation(Z,C).


% G has_function F if a curator says so (with evidence)
has_function(G,F) :- asserted_has_function(G,F).

% inference using GO
has_function(G,F) :- is_a(F2,F),has_function(G,F2).
has_function(G,F) :- part_of(F2,F),has_function(G,F2).

has_function(G,F) :- gene(G), function(F), not in_organism_lacking_function(G,F).

% G lacks_function F if a curator says so
lacks_function(G,F) :- asserted_lacks_function(G,F).

% negation causes downward propagation
lacks_function(G,F) :- is_a(F,F2),lacks_function(G,F2).
lacks_function(G,F) :- part_of(F,F2),lacks_function(G,F2).

% propagation DOWN the phylogenetic tree:
% functions propagate down unless there was an ancestral loss
has_function(G,F) :- descended_from(G,G2),has_function(G2,F), not lacks_function(G2,F).

% propagation UP the phylogenetic tree:
% here we would ideally have a probabilistic model.
% we assume potential for annotation 
has_function(G,F) v descendents_independently_evolved(G,F) :-
        has_function(GC1,F),descended_from_by_speciation(GC1,G),
        has_function(GC2,F),descended_from_by_speciation(GC2,G),
        nad(GC1,GC2).


% NEGATED propagation UP the phylogenetic tree:
lacks_function(G,F) v descendants_independently_lost(G,F) :-
        lacks_function(GC1,F),descended_from_by_speciation(GC1,G),
        lacks_function(GC2,F),descended_from_by_speciation(GC2,G),
        nad(GC1,GC2).

% suggest annotations for leaf nodes
suggested_has_function_annotation(G,F) :- has_function(G,F), not asserted_has_function(G,F), leaf_node(G).

% TEST DATA
descended_from(a,ab).
descended_from(b,ab).
descended_from(c,cd).
descended_from(d,cd).

descended_from(ab,abcde).
descended_from(cd,abcde).







